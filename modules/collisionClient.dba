REM *** Include File: modules\meshClient.dba ***
REM Created: 7/4/2010 11:02:33 AM
REM
REM Included in Project: C:\Program Files\The Game Creators\Dark Basic Professional\Projects\worldofomen\worldofomen.dbpro
REM

collisionClientSetup:

   #constant COLLISION_FUNCTION_FAIL -1
   #constant COLLISION_FUNCTION_OK 1

return

function collisionClientSet(dbpid as integer, collisionType as integer, collisionGroup as integer)

   if dbpid = 0
      exitfunction COLLISION_FUNCTION_FAIL
   else
      if object exist(dbpid) = 0 then exitfunction COLLISION_FUNCTION_FAIL
   endif
   if collisionType < COLLISION_TYPE_POLY or collisionType > COLLISION_TYPE_NONE then exitfunction COLLISION_FUNCTION_FAIL

   select collisionType
      case COLLISION_TYPE_POLY:
         SC_setupObject dbpid , collisionGroup , 0
      endcase
      case COLLISION_TYPE_SPHERE:
         SC_setupObject dbpid , collisionGroup , 1
      endcase
      case COLLISION_TYPE_BOX:
         SC_setupObject dbpid , collisionGroup , 2
      endcase
      case COLLISION_TYPE_COMPLEX:
         SC_setupComplexObject dbpid , collisionGroup , 2
      endcase
      case COLLISION_TYPE_NONE
         collisionClientDelete(dbpid)
      endcase
   endselect

endfunction COLLISION_FUNCTION_OK

function collisionClientDelete(dbpid as integer)

   if dbpid = 0
      exitfunction COLLISION_FUNCTION_FAIL
   else
      if object exist(dbpid) = 0 then exitfunction COLLISION_FUNCTION_FAIL
   endif

   sc_removeObject dbpid

endfunction COLLISION_FUNCTION_OK

function collisionClientSlide(dbpid as integer,cGroup as integer,oldx# as float,oldy# as float,oldz# as float,x# as float,y# as float,z# as float,gravityOn as boolean)

   local ground as integer
   ground = COLLISION_FUNCTION_FAIL

   if dbpid = 0
      exitfunction COLLISION_FUNCTION_FAIL
   else
      if object exist(dbpid) = 0 then exitfunction COLLISION_FUNCTION_FAIL
   endif

   rem handle vertical movement
   mobRadius# = (object size y(dbpid,1) * 0.5)

   if gravityOn = 1

       vy# = systemSharedFrameMoved(SYSTEM_GRAVITY)
       collide = sc_SphereCastGroup(cGroup,oldx#,oldy#,oldz#,oldx#,y#+vy#,oldz#,mobRadius#,dbpid)

       if collide>0
          oldy# = sc_getStaticCollisionY()
          ny# = sc_getCollisionNormalY()
          if ny# < 0.0
            REM MOBILE HAS HIT A CEILING INSTEAD OF FLOOR, SO REVERSE THEIR VELOCITY
            REM WE WILL ADD THIS LATER, SINCE IT REQUIRES OBJECT-SPECIFIC ATTRIBUTE MODIFICATION
            ground = 0
          else
            ground = 1
          endif
          vy# = 0
       else
          rem nothing below player, not on ground, add vertical speed to player
          oldy# = y# + vy#
          ground = 0
       endif
       newy# = oldy#
   else
       ground = 1
       vy# = 0
       newy# = y#
   endif

   rem handle horizontal movement as sliding
   collide = sc_SphereSlideGroup(cGroup,oldx#,oldy#,oldz#,x#,newy#,z#,mobRadius#,dbpid)

   if collide>0
      rem if hit, reposition player, halt movement vector
      x# = sc_getCollisionSlideX()
      newy# = sc_getCollisionSlideY()
      z# = sc_getCollisionSlideZ()
      vx# = 0
      vz# = 0
   endif

   if newy# < COLLISION_FALLOUT_Y
        newy# = COLLISION_RESET_Y
   endif

   rem position the player
   position object dbpid,x#,newy#,z#

   sc_updateObject dbpid

endfunction ground

function collisionClientFall(dbpid as integer,cGroup as integer,oldx# as float,oldy# as float,oldz# as float,x# as float,y# as float,z# as float,gravityOn as boolean)

   local ground as integer
   ground = COLLISION_FUNCTION_FAIL

   if dbpid = 0
      exitfunction COLLISION_FUNCTION_FAIL
   else
      if object exist(dbpid) = 0 then exitfunction COLLISION_FUNCTION_FAIL
   endif

   rem handle vertical movement
   mobRadius# = (object size y(dbpid,1) * 0.5)

   if gravityOn = 1

       vy# = systemSharedFrameMoved(SYSTEM_GRAVITY)
       collide = sc_SphereCastGroup(cGroup,oldx#,oldy#,oldz#,oldx#,y#+vy#,oldz#,mobRadius#,dbpid)

       if collide>0
          oldy# = sc_getStaticCollisionY()
          ny# = sc_getCollisionNormalY()
          if ny# < 0.0
            REM MOBILE HAS HIT A CEILING INSTEAD OF FLOOR, SO REVERSE THEIR VELOCITY
            REM WE WILL ADD THIS LATER, SINCE IT REQUIRES OBJECT-SPECIFIC ATTRIBUTE MODIFICATION
            ground = 0
          else
            ground = 1
          endif
          vy# = 0
       else
          rem nothing below player, not on ground, add vertical speed to player
          oldy# = y# + vy#
          ground = 0
       endif
       newy# = oldy#
   else
       ground = 1
       vy# = 0
       newy# = y#
   endif

   if newy# < COLLISION_FALLOUT_Y
        newy# = COLLISION_RESET_Y
   endif

   rem position the player
   position object dbpid,x#,newy#,z#

   sc_updateObject dbpid

endfunction ground
