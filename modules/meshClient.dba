REM *** Include File: modules\meshClient.dba ***
REM Created: 7/4/2010 11:02:33 AM
REM
REM Included in Project: C:\Program Files\The Game Creators\Dark Basic Professional\Projects\worldofomen\worldofomen.dbpro
REM

meshClientSetup:

   #constant MESH_FUNCTION_FAIL -1
   #constant MESH_FUNCTION_OK 1

   #constant MESH_ANIMDATA_PREFIX "anim_"
   #constant MESH_ANIMDATA_START "_start"
   #constant MESH_ANIMDATA_STOP "_stop"
   #constant MESH_ANIMDATA_SPEED "_speed"
   #constant MESH_ANIMDATA_SWITCH "_switch"
   #constant MESH_ANIM_LOOP 0
   #constant MESH_ANIM_PLAY 1

   global MESH_DEFAULT_FPS as float
   MESH_DEFAULT_FPS = val(dataSharedGetField("meshClient", "meshDefaultFPS", "24.0", DATA_CREATE))


return

function meshClientLoad(myPath$, mydbpid)

   REM LOADS AN ASSET FOR LATER INSTANTIATION

   if file exist(myPath$) = 0 then exitfunction MESH_FUNCTION_FAIL
   load object myPath$, mydbpid, 1
   `hide object mydbpid
   `exclude object on mydbpid

endfunction MESH_FUNCTION_OK

function meshClientDestroy(mytid as integer)

   mydbpid = intval(dataSharedGetFieldFast(mytid, "dbpid"))
   if mydbpid = 0 then exitfunction
   if object exist(mydbpid) = 0 then exitfunction
   
   delete object mydbpid
   
   dataSharedUpdateFieldFast(mytid, "dbpid", DATA_KEY_UNDEFINED)

endfunction

function meshClientCreate(mytid as integer)

   REM INSTANTIATE A MESH INTO THE WORLD
   REM ASSUMES THE TABLE HAS BEEN
   REM PRE-POPULATED WITH rpgidmesh

   mydbpid = intval(dataSharedGetFieldFast(mytid,"dbpid"))
   myrpgidmesh = intval(dataSharedGetFieldFast(mytid,"rpgidmesh"))
   if mydbpid = 0
      mydbpid = systemSharedGetFree(SYSTEM_TYPE_OBJECT, SYSTEM_SEARCH_REUSE)
      dataSharedUpdateFieldFast(mytid, "dbpid", str$(mydbpid))
   else
      if object exist(mydbpid)
         delete object mydbpid
      endif
   endif

   REM WE'RE GOING TO MAKE A "HOLDER" OBJECT TO
   REM GUARANTEE WE HAVE THIS OBJECT RESERVED
   REM WE'RE ALSO GOING TO MAKE IT HIGHLY
   REM VISIBLE TO MAKE SOURCE MESH ISSUES
   REM VERY EASY TO SEE

   make object cylinder mydbpid, 10
   scale object mydbpid, 100, 5000, 100
   color object mydbpid, rgb(255,0,128)

   myPath$ = dataSharedGetLibraryPath(myrpgidmesh)
   mysourcedbpid = dataSharedGetAssetID(myrpgidmesh)
   debugWrite(DEBUGINFO,"meshClientCreate: target dbpid found for cloning " + str$(mydbpid))
   debugWrite(DEBUGINFO,"meshClientCreate: source dbpid found for cloning " + str$(mysourcedbpid))

   if mysourcedbpid < 1 then exitfunction MESH_FUNCTION_FAIL
   if object exist(mysourcedbpid) = 0 then exitfunction MESH_FUNCTION_FAIL

   REM A MESH SOURCE ERROR PRIOR TO THIS POINT WOULD LEAVE
   REM THE HOLDER OJBECT IN PLACE
   if object exist(mydbpid) then delete object mydbpid
   clone object mydbpid, mysourcedbpid, 1

   datafilename$ = myPath$ + DATA_FILE_EXT

   if file exist(datafilename$)

      debugWrite(DEBUGINFO,"meshClientCreate: data file for mesh exists: " + datafilename$)
      REM MESH HAS ITS OWN DATA FILE
      REM LOAD TABLE IF NOT ALREADY LOADED
      if dataSharedTableExist(datafilename$) = DATA_FUNCTION_FAIL
         debugWrite(DEBUGINFO,"meshClientCreate: data file not previously loaded, loading and applying: " + datafilename$)
         dataSharedLoadTable(datafilename$,datafilename$)
      else
         debugWrite(DEBUGINFO,"meshClientCreate: data file already loaded, applying: " + datafilename$)
      endif

      REM APPLY THE DATA FILE TO THE ASSOCIATED
      REM DATAOBJECT ARRAY
      mytemplatetid = dataSharedTableExist(datafilename$)
      dataSharedCopyTable(mytemplatetid, mytid)
      dataSharedUpdateFieldFast(mytid, "meshdata",datafilename$)

   endif
   
   REM APPLY TRANSFORMS
   posx# = val(dataSharedGetFieldFast(mytid,"posx"))
   posy# = val(dataSharedGetFieldFast(mytid,"posy"))
   posz# = val(dataSharedGetFieldFast(mytid,"posz"))

   rotx# = val(dataSharedGetFieldFast(mytid,"rotx"))
   roty# = val(dataSharedGetFieldFast(mytid,"roty"))
   rotz# = val(dataSharedGetFieldFast(mytid,"rotz"))
   
   sclx# = val(dataSharedGetFieldFast(mytid,"sclx"))
   scly# = val(dataSharedGetFieldFast(mytid,"scly"))
   sclz# = val(dataSharedGetFieldFast(mytid,"sclz"))
   
   colType = intval(dataSharedGetFieldFast(mytid,"collisiontype"))
   colGroup = intval(dataSharedGetFieldFast(mytid,"collisiongroup"))
   debugWrite(DEBUGINFO,"collisionType found to be " + str$(colType))
   debugWrite(DEBUGINFO,"collisionGroup found to be " + str$(colGroup))

   position object mydbpid, posx#,posy#,posz#
   if sclx# <> 1 or scly# <> 1 or sclz# <> 1
      if sclx# > 0 and scly# > 0 and sclz# > 0
         scale object mydbpid, (sclx# * 100.0),(scly# * 100.0),(sclz# * 100.0)
      endif
   endif
   myval = collisionClientSet(mydbpid,colType,colGroup)
   debugWrite(DEBUGINFO,"collision Setup Result: " + str$(myval))

endfunction MESH_FUNCTION_OK

function meshClientAnimate(mytid as integer, animName as string, loopPlay as boolean)

   local retval as integer
   local mydbpid as integer
   local frameStart as integer
   local frameEnd as integer
   local mymeshdata as string
   local caseAnimName as string

   retval = MESH_FUNCTION_FAIL
   if animName = "" then exitfunction retval
   mymeshdata = dataSharedGetFieldFast(mytid,"meshdata")
   if mymeshdata = "" or mymeshdata = DATA_KEY_UNDEFINED then exitfunction retval
   
   mydbpid = intval(dataSharedGetFieldFast(mytid,"dbpid"))
   if mydbpid = 0 then exitfunction retval
   if object exist(mydbpid) = 0 then exitfunction retval
   
   myAnimSwitchCase = intval(dataSharedGetFieldFast(mytid,"animSwitchCase"))
   myAnimSwitchTime = intval(dataSharedGetFieldFast(mytid,"animSwitchTime"))

   REM IF REQUESTING AN ANIMATION GROUP, CHECK IF TIME TO SWITCH
   if fast right$(animName,1)="1"
      caseAnimName = replace$(animName,"1",str$(myAnimSwitchCase))
      if systemGameTimeMilliseconds > myAnimSwitchTime
         myAnimSwitchTime = systemGameTimeMilliseconds + intval(dataSharedGetField(mymeshdata, MESH_ANIMDATA_PREFIX + caseAnimName + MESH_ANIMDATA_SWITCH, DATA_KEY_UNDEFINED, DATA_NO_CREATE))
         inc myAnimSwitchCase
         caseAnimName = replace$(animName,"1",str$(myAnimSwitchCase))
         dataSharedUpdateFieldFast(mytid, "animSwitchCase", str$(myAnimSwitchCase))
         dataSharedUpdateFieldFast(mytid, "animSwitchTime", str$(myAnimSwitchTime))
      endif
     if dataSharedGetField(mymeshdata, MESH_ANIMDATA_PREFIX + caseAnimName + MESH_ANIMDATA_START, DATA_KEY_UNDEFINED, DATA_NO_CREATE) = DATA_KEY_UNDEFINED
        dataSharedUpdateFieldFast(mytid, "animSwitchCase", "1")
     else
        animName = caseAnimName
     endif
   else
      dataSharedUpdateFieldFast(mytid, "animSwitchCase", "1")
   endif

   temp$ = dataSharedGetField(mymeshdata, MESH_ANIMDATA_PREFIX + animName + MESH_ANIMDATA_START, DATA_KEY_UNDEFINED, DATA_NO_CREATE)
   if temp$ <> DATA_KEY_UNDEFINED
      frameStart = intval(temp$)
      temp$ = dataSharedGetField(mymeshdata, MESH_ANIMDATA_PREFIX + animName + MESH_ANIMDATA_STOP, DATA_KEY_UNDEFINED, DATA_NO_CREATE)
      if temp$ <> DATA_KEY_UNDEFINED
         frameEnd = intval(temp$)
         retval = MESH_FUNCTION_OK
         temp$ = dataSharedGetField(mymeshdata, MESH_ANIMDATA_PREFIX + animName + MESH_ANIMDATA_SPEED, DATA_KEY_UNDEFINED, DATA_NO_CREATE)

         REM ONLY LOOP OR PLAY IF NOT ALREADY
         REM PLAYING REQUESTED ANIMATION
         temp = object frame(mydbpid)
         if temp < frameStart or temp > frameEnd

            REM SET SPEED IF AVAILABLE
            if temp$ = DATA_KEY_UNDEFINED
               dataSharedUpdateFieldFast(mytid, "animspeed", "1.0")
            else
               dataSharedUpdateFieldFast(mytid, "animspeed", temp$)
            endif

            if loopPlay = MESH_ANIM_LOOP
               loop object mydbpid, frameStart, frameEnd
            else
               play object mydbpid, frameStart, frameEnd
            endif

         endif
      endif
   endif

endfunction retval

function meshClientStopAnimation(idx)
   
   local mydbpid as integer
  
   mydbpid = intval(dataSharedGetFieldFast(mytid,"dbpid"))
   if mydbpid = 0 then exitfunction MESH_FUNCTION_FAIL
   if object exist(mydbpid) = 0 then exitfunction MESH_FUNCTION_FAIL
   stop object mydbpid
   set object frame mydbpid, 1

endfunction MESH_FUNCTION_OK

function meshClientRegulate(mytid as integer)

   REM REGULATE THE ANIMATION SPEED
   local mydbpid as integer
   local myanimspeed as float
  
   mydbpid = intval(dataSharedGetFieldFast(mytid,"dbpid"))
   if mydbpid = 0 then exitfunction MESH_FUNCTION_FAIL
   if object exist(mydbpid) = 0 then exitfunction MESH_FUNCTION_FAIL
   myanimspeed = val(dataSharedGetFieldFast(mytid,"animspeed"))
   
   REM MESHES ARE SAVED WITH MESH_DEFAULT_FPS ANIMATION SPEED
   speed# = (MESH_DEFAULT_FPS / systemGameFPS) * myanimspeed * 100.0
   speed# = clamp(speed#, 1.0, 100.0)
   set object speed mydbpid, speed#

endfunction MESH_FUNCTION_OK


