REM *** Include File: modules\commandClient.dba ***
REM Created: 7/4/2010 11:00:01 AM
REM
REM Included in Project: C:\Program Files\The Game Creators\Dark Basic Professional\Projects\worldofomen\worldofomen.dbpro
REM

function commandRun(cmd$ as string)

   local cmdFound as boolean

   split string cmd$,COMMAND_CHAR_ARGSEP
   cmdAction$ = lower$(get split word$(1))

   REM ARGUMENTS WITH EMBEDDED ARGUMENT SEPERATOR
   REM CHARACTERS WILL BE TRANSPOSED AS NEEDED
   REM USING THIS COMMAND:
   REM arg$ = replace all$(arg$,COMMAND_CHAR_ARGEMB,COMMAND_CHAR_ARGSEP)

   cmdFound = 0
   select cmdAction$

      case "/exit"
         cmdFound = 1
         systemSharedGameModeAdd(SYSTEM_MODE_QUITTING)
      endcase

      case "/loc"
         cmdFound = 1
            mydbpid = intval(dataSharedGetFieldFast(tidPlayer, "dbpid"))
            if mydbpid = 0 then exitfunction
            if object exist(mydbpid) = 0 then exitfunction
            msg$="x:" + systemSharedRound$(object position x(mydbpid),2) + ",y:" + systemSharedRound$(object position y(mydbpid),2) + ",z:" + systemSharedRound$(object position z(mydbpid),2)
            guiClientUpdateChatRcv(msg$)
      endcase

      case "/listen"
         cmdFound = 1
         channel$ = upper$(get split word$(2))
         netClientChangeChannels(NETC_LISTEN, channel$)
      endcase

      case "/mute"
         cmdFound = 1
         channel$ = upper$(get split word$(2))
         netClientChangeChannels(NETC_MUTE, channel$)
      endcase

      case "/setchannel"
         cmdFound = 1
         netClientCurrentChannel = upper$(get split word$(2))
      endcase
      
      case "/createaccount"
         cmdFound = 1
         accountName$ = dataSharedGetField( DATA_TABLE_INPUT, "inputCreateAccountName", DATA_KEY_UNDEFINED, DATA_NO_CREATE)
         password$ = dataSharedGetField( DATA_TABLE_INPUT, "inputCreateAccountPassword", DATA_KEY_UNDEFINED, DATA_NO_CREATE)
         debugWrite(DEBUGINFO,"Create account for " + accountName$ + "/" + password$)
         encryptSetKey(systemGameTitle$)
         password$ = EncryptString(password$)
         netClientSendCreateAccount(accountName$, password$) : accountName$ = "" : password$ = ""
         dataSharedUpdateField(DATA_TABLE_INPUT, "inputCreateAccountPassword", DATA_KEY_UNDEFINED)
      endcase
      
      case "/logon"
         cmdFound = 1
         accountName$ = dataSharedGetField( DATA_TABLE_INPUT, "inputLogonAccountName", DATA_KEY_UNDEFINED, DATA_NO_CREATE)
         netClientSendLogonAlpha(accountName$)
      endcase
      
      case "/switchtowindow"
         cmdFound = 1
         guiClientSwitchToWindow(get split word$(2))
      endcase

      case "/hidewindow"
         cmdFound = 1
         guiClientKillWindow(get split word$(2))
      endcase

      case "/showwindow"
         cmdFound = 1
         guiClientShowWindow(get split word$(2))
      endcase

      case "/togglewindow"
         cmdFound = 1
         guiClientToggleWindow(get split word$(2))
      endcase


   endselect

   if cmdFound = 0
      REM NOT A CLIENT COMMAND, SEND TO SERVER
      netClientSendCommand(cmd$)
      debugWrite(DEBUGINFO,"Command Not Found, Sending to Server")
   endif


endfunction
