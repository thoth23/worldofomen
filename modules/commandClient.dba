REM *** Include File: modules\commandClient.dba ***
REM Created: 7/4/2010 11:00:01 AM
REM
REM Included in Project: C:\Program Files\The Game Creators\Dark Basic Professional\Projects\worldofomen\worldofomen.dbpro
REM

function commandRun(cmd$ as string)

   local cmdFound as boolean

   split string cmd$,COMMAND_CHAR_ARGSEP
   cmdAction$ = lower$(get split word$(1))

   REM ARGUMENTS WITH EMBEDDED ARGUMENT SEPERATOR
   REM CHARACTERS WILL BE TRANSPOSED AS NEEDED
   REM USING THIS COMMAND:
   REM arg$ = replace all$(arg$,COMMAND_CHAR_ARGEMB,COMMAND_CHAR_ARGSEP)

   cmdFound = 0
   select cmdAction$

      case "/exit"
         cmdFound = 1
         systemSharedGameModeAdd(SYSTEM_MODE_QUITTING)
      endcase

      case "/loc"
         cmdFound = 1
         msg$="x:" + systemRound$(object position x(5),2) + ",y:" + systemRound$(object position y(5),2) + ",z:" + systemRound$(object position z(5),2)
         keyboardUpdateChatReceive(msg$)
      endcase

      case "/listen"
         cmdFound = 1
         channel$ = upper$(get split word$(2))
         netClientChangeChannels(NETC_LISTEN, channel$)
      endcase

      case "/mute"
         cmdFound = 1
         channel$ = upper$(get split word$(2))
         netClientChangeChannels(NETC_MUTE, channel$)
      endcase

      case "/setchannel"
         cmdfound = 1
         netClientCurrentChannel = upper$(get split word$(2))
      endcase

   endselect

   if cmdFound = 0
      REM NOT A CLIENT COMMAND, SEND TO SERVER
      netClientSendCommand(cmd$)
      debugWrite(DEBUGINFO,"Command Not Found, Sending to Server")
   endif


endfunction
