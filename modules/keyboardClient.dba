REM *** Include File: modules\keyboardClient.dba ***
REM Created: 7/4/2010 11:05:25 AM
REM
REM Included in Project: C:\Program Files\The Game Creators\Dark Basic Professional\Projects\worldofomen\worldofomen.dbpro
REM

keyboardClientSetup:

   global keyboardKeyDown as boolean
   global keyboardNextKeyRepeat as float
   global keyboardRepeatDelay as float
   keyboardRepeatDelay = intval(dataSharedGetField("keyboardClient", "keyboardRepeatDelay", "250", DATA_CREATE))

   global keyboardCursorBlinkState as boolean
   global keyboardCursorNextBlink as integer
   global keyboardCursorBlinkDelay as float
   keyboardCursorBlinkDelay = intval(dataSharedGetField("keyboardClient", "keyboardCursorBlinkDelay", "333", DATA_CREATE))

   global keyboardShowLines as integer
   keyboardShowLines = int(screen height()/(text size() * 2.0))
   global keyboardChatInput as string
   global dim keyboardChatRecvd() as string
   add to queue keyboardChatRecvd()

   REM KEYBOARD INPUT MAP - SCANCODES
   global KEYBOARD_KEY_OPEN_CHAT as integer
   KEYBOARD_KEY_OPEN_CHAT = intval(dataSharedGetField("keyboardClient", "keyboardKeyOpenChat", "53", DATA_CREATE))
   global KEYBOARD_KEY_SEND_CHAT as integer
   KEYBOARD_KEY_SEND_CHAT = intval(dataSharedGetField("keyboardClient", "keyboardKeySendChat", "28", DATA_CREATE))
   global KEYBOARD_KEY_DELETE as integer
   KEYBOARD_KEY_DELETE = intval(dataSharedGetField("keyboardClient", "keyboardKeyOpenDelete", "14", DATA_CREATE))
   global KEYBOARD_KEY_CLOSE_CHAT as integer
   KEYBOARD_KEY_CLOSE_CHAT = intval(dataSharedGetField("keyboardClient", "keyboardKeyCloseChat", "1", DATA_CREATE))
   global KEYBOARD_KEY_QUIT_GAME as integer
   KEYBOARD_KEY_QUIT_GAME = intval(dataSharedGetField("keyboardClient", "keyboardKeyQuitGame", "1", DATA_CREATE))

   disable escapekey

return

function keyboardClientPollTop()

   if keyboardKeyDown = 1
       if timer() > keyboardNextKeyRepeat
            keyboardKeyDown = 0
            keyboardNextKeyRepeat = timer() + keyboardRepeatDelay
       endif
   endif

   if scanCode() = 0
      keyboardKeyDown = 0
   endif

endfunction

function keyboardClientPollBottom()

   if keyboardKeyDown = 0

      if scanCode() <> 0
         keyboardKeyDown = 1
         keyboardNextKeyRepeat = timer() + keyboardRepeatDelay
      endif

      if (systemMode && SYSTEM_MODE_CHAT_ACTIVE) = 0

         REM CHAT MODE IS NOT ACTIVE

         if keyState(KEYBOARD_KEY_OPEN_CHAT) = 1
            systemSharedGameModeAdd(SYSTEM_MODE_CHAT_ACTIVE)
            keyboardChatInput = ""
            clear entry buffer
         endif

         if keyState(KEYBOARD_KEY_QUIT_GAME) = 1
            systemSharedGameModeAdd(SYSTEM_MODE_QUITTING)
         endif

      else

         REM CHAT MODE IS ACTIVE

         keyboardAddBuffer = 1
         if keyState(KEYBOARD_KEY_SEND_CHAT) = 1
            keyboardChatInput = trim$(keyboardChatInput)
            if fast len(keyboardChatInput) > 0
               if fast left$(keyboardChatInput,1) = COMMAND_CHAR_START
                  commandSharedParse(keyboardChatInput)
               else
                  sendChannel$ = netClientCurrentChannel
                  if fast len(keyboardChatInput) > 2
                     chSelector$ = upper$(fast left$(keyboardChatInput,2))
                     selectorFound = 0
                     select chSelector$
                        case "B "
                           selectorFound = 1
                           sendChannel$ = "BROADCAST"
                        endcase
                        case "L "
                           selectorFound = 1
                           sendChannel$ = "LOCAL"
                        endcase
                        case "T "
                           selectorFound = 1
                           sendChannel$ = "TEAM"
                        endcase
                     endselect
                     if selectorFound = 1
                        keyboardChatInput = fast right$(keyboardChatInput,len(keyboardChatInput)-2)
                     endif
                  endif
                  netClientSendChat(sendChannel$,keyboardChatInput)
               endif
            else
               keyboardUpdateChatReceive("")
            endif
            keyboardAddBuffer = 0
            keyboardChatInput = ""
            clear entry buffer
         endif

         if keyState(KEYBOARD_KEY_CLOSE_CHAT) = 1
            systemSharedGameModeRemove(SYSTEM_MODE_CHAT_ACTIVE)
            keyboardAddBuffer = 0
            keyboardChatInput = ""
            clear entry buffer
         endif

         if keyState(KEYBOARD_KEY_DELETE) = 1
            if len(keyboardChatInput) > 0
               keyboardAddBuffer = 0
               keyboardChatInput = fast left$(keyboardChatInput, len(keyboardChatInput) - 1)
               clear entry buffer
            endif
         endif

         if keyboardAddBuffer = 1
            keyboardChatInput = keyboardChatInput + keyBoardSharedSanitizeCharacter(entry$())
            clear entry buffer
         endif

      endif

   endif

endfunction

function keyboardUpdateChatReceive(message$ as string)

   add to queue keyboardChatRecvd()
   keyboardChatRecvd() = message$

endfunction

function keyboardClientShowChat()

   REM TEMPORARY FUNCTION TO SHOW CHAT
   REM TEXT USING THE BUILT-IN 2D TEXT

   local startLine as integer
   local stopLine as integer
   local currentLine as integer

   if (systemMode && SYSTEM_MODE_CHAT_ACTIVE) <> 0
      startLine = array count(keyboardChatRecvd())
      stopLine = max(1,(startLine - keyboardShowLines))
      currentLine = keyboardShowLines
      for i = startLine to stopLine step -1
         if keyboardChatRecvd(i) <> ""
            text 5,(currentLine * text size()), keyboardChatRecvd(i)
            dec currentLine
         endif
      next

      text 5, ((keyboardShowLines+1) * text size()),"----------------"

      text 5, ((keyboardShowLines+2) * text size()),"["+ netClientCurrentChannel +"] " + keyboardChatInput

      if timer() > keyboardCursorNextBlink
         keyboardCursorNextBlink = timer() + keyboardCursorBlinkDelay
         keyboardCursorBlinkState = 1-keyboardCursorBlinkState
      endif

      if keyboardCursorBlinkState = 1
         text 5 + text width(keyboardChatInput) + text width(netClientCurrentChannel+"][ "), ((keyboardShowLines+2) * text size()),"_"
      endif
   endif

endfunction
