REM *** Include File: modules\commandClient.dba ***
REM Created: 7/4/2010 11:00:01 AM
REM
REM Included in Project: C:\Program Files\The Game Creators\Dark Basic Professional\Projects\worldofomen\worldofomen.dbpro
REM

function commandRun(cmd$ as string)

   split string cmd$,COMMAND_CHAR_ARGSEP
   cmdAction$ = lower$(get split word$(1))

   REM ARGUMENTS WITH EMBEDDED ARGUMENT SEPERATOR
   REM CHARACTERS WILL BE TRANSPOSED AS NEEDED
   REM USING THIS COMMAND:
   REM arg$ = replace all$(arg$,COMMAND_CHAR_ARGEMB,COMMAND_CHAR_ARGSEP)

   select cmdAction$

      case "/exit"
         systemSharedGameModeAdd(SYSTEM_MODE_QUITTING)
      endcase

      case "/loc"
            mydbpid = intval(dataSharedGetFieldFast(tidPlayer, "dbpid"))
            if mydbpid = 0 then exitfunction
            if object exist(mydbpid) = 0 then exitfunction
            msg$="x:" + systemSharedRound$(object position x(mydbpid),2) + ",y:" + systemSharedRound$(object position y(mydbpid),2) + ",z:" + systemSharedRound$(object position z(mydbpid),2)
            guiClientUpdateChatRcv(msg$)
      endcase

      case "/listen"
         channel$ = upper$(get split word$(2))
         netClientChangeChannels(NETC_LISTEN, channel$)
      endcase

      case "/mute"
         channel$ = upper$(get split word$(2))
         netClientChangeChannels(NETC_MUTE, channel$)
      endcase

      case "/setchannel"
         netClientCurrentChannel = upper$(get split word$(2))
      endcase
      
      case "/createaccount"
         guiClientHideWindow("windowCreateAccount")
         accountName$ = dataSharedGetField( DATA_TABLE_INPUT, "inputCreateAccountName", DATA_KEY_UNDEFINED, DATA_NO_CREATE)
         password$ = dataSharedGetField( DATA_TABLE_INPUT, "inputCreateAccountPassword", DATA_KEY_UNDEFINED, DATA_NO_CREATE)
         debugWrite(DEBUGINFO,"Create account for " + accountName$ + "/" + password$)
         encryptSetKey(systemGameTitle$)
         password$ = EncryptString(password$)
         netClientSendCreateAccount(accountName$, password$) : accountName$ = "" : password$ = ""
         dataSharedUpdateField(DATA_TABLE_INPUT, "inputCreateAccountPassword", DATA_KEY_UNDEFINED)
      endcase
      
      case "/logon"
         guiClientHideWindow("windowLogon")
         accountName$ = dataSharedGetField( DATA_TABLE_INPUT, "inputLogonAccountName", DATA_KEY_UNDEFINED, DATA_NO_CREATE)
         netClientSendLogonAlpha(accountName$)
      endcase
      
      case "/logoff"
         guiClientSwitchToWindow("windowLogon")
         REM MORE STATE MANAGEMENT WILL BE DONE HERE
      endcase
      
      case "/switchtowindow"
         guiClientSwitchToWindow(get split word$(2))
      endcase

      case "/hidewindow"
         guiClientHideWindow(get split word$(2))
      endcase

      case "/showwindow"
         guiClientShowWindow(get split word$(2))
      endcase

      case "/togglewindow"
         guiClientToggleWindow(get split word$(2))
      endcase
      
      case "/killwindow"
         guiClientKillWindow(get split word$(2))
      endcase
      case "/pressbutton"
         guiClientPressButtonNoCommand(get split word$(2))
      endcase
      
      case "/switchclass"
            temp = intval(get split word$(2))
            guiClientSetInputField("inputCreateAvatarClass", str$(temp))
            guiClientSetTextArea(guiClientGetGadgetID("labelCreateAvatarClassName"),guiClientGetLanguageField("LanguageTagClassName"+str$(temp)),0,0)
            guiClientSetTextArea(guiClientGetGadgetID("labelCreateAvatarClassDescription"),guiClientGetLanguageField("LanguageTagClassDescription"+str$(temp)),0,0.9)
            temp$ = guiClientGetLanguageField("LanguageTagClassAttributes"+str$(temp))
            split string temp$, "\"
            guiClientSetTextArea(guiClientGetGadgetID("labelCreateAvatarDamage"),get split word$(1),0,0)
            guiClientSetTextArea(guiClientGetGadgetID("labelCreateAvatarHealth"),get split word$(2),0,0)
            guiClientSetTextArea(guiClientGetGadgetID("labelCreateAvatarAccuracy"),get split word$(3),0,0)
            guiClientSetTextArea(guiClientGetGadgetID("labelCreateAvatarDefense"),get split word$(4),0,0)
            guiClientSetTextArea(guiClientGetGadgetID("labelCreateAvatarRepair"),get split word$(5),0,0)
            guiClientSetTextArea(guiClientGetGadgetID("labelCreateAvatarBuff"),get split word$(6),0,0)
            guiClientSetTextArea(guiClientGetGadgetID("labelCreateAvatarDebuff"),get split word$(7),0,0)
      endcase
      
      case "/switchpowersource"
         temp = intval(get split word$(2))
         guiClientSetInputField("inputCreateAvatarPowerSource", str$(temp))
         guiClientSetTextArea(guiClientGetGadgetID("labelCreateAvatarPowerSourceName"),guiClientGetLanguageField("LanguageTagPowerSourceName"+str$(temp)),0,0)
         guiClientSetTextArea(guiClientGetGadgetID("labelCreateAvatarPowerSourceDescription"),guiClientGetLanguageField("LanguageTagPowerSourceDescription"+str$(temp)),0,0.9)
      endcase
     
      
      case "/switchmorph"
         REM SWITCHMORPH IS ONLY USED IN AVATAR CREATION
         REM SO WE'RE JUST GOING TO USE A STRING LIST FOR
         REM THE MORPHS, THESE WILL BE MADE INTO
         REM PERMANENT ATTRIBUTES WHEN RECEIVED BACK
         REM FROM THE SERVER
         mytid = intval(get split word$(2))
         mymorphid = intval(get split word$(3))
         mymorphlist$ = dataSharedGetField(DATA_TABLE_INPUT, "inputCreateAvatarMorphList", "", DATA_CREATE)
         if instr(mymorphlist$,str$(mymorphid)) = 0
            REM NOT APPLIED YET, ADD IT
            meshClientApplyMorphTarget(mytid, mymorphid,MORPH_TYPE_ADD)
            mymorphlist$ = systemSharedFieldAdd(mymorphlist$, str$(mymorphid), DATA_ARGSEP)
         else
            REM ALREADY THERE, REMOVE IT
            meshClientApplyMorphTarget(mytid, mymorphid,MORPH_TYPE_REMOVE)
            mymorphlist$ = systemSharedFieldRemove(mymorphlist$, str$(mymorphid), DATA_ARGSEP)
         endif
         dataSharedUpdateField(DATA_TABLE_INPUT, "inputCreateAvatarMorphList", mymorphlist$)
      endcase
 
       case "/switchmaterial"
         REM SWITCHMATERIAL IS ONLY USED IN AVATAR CREATION
         REM SO WE'RE JUST GOING TO USE A STRING LIST FOR
         REM THE MATERIALS, THESE WILL BE MADE INTO
         REM PERMANENT ATTRIBUTES WHEN RECEIVED BACK
         REM FROM THE SERVER
         mytid = intval(get split word$(2))
         mymatid = intval(get split word$(3))
         mymatlist$ = dataSharedGetField(DATA_TABLE_INPUT, "inputCreateAvatarMaterialList", "", DATA_CREATE)
         if instr(mymatlist$,str$(mymatid)) = 0
            REM NOT APPLIED YET, ADD IT
            materialClientApply(mytid, mymatid)
            mymatlist$ = systemSharedFieldAdd(mymatlist$, str$(mymatid), DATA_ARGSEP)
         endif
         dataSharedUpdateField(DATA_TABLE_INPUT, "inputCreateAvatarMaterialList", mymatlist$)
      endcase
     
      case "/createlocalplayer"
         mymeshid = intval(dataSharedGetField(DATA_TABLE_LIBRARY + DATA_REVERSE_SUFFIX, MESH_AVATAR_PATH + PLAYER_DEFAULT_MESH, DATA_KEY_UNDEFINED, DATA_NO_CREATE))
         debugWrite(DEBUGINFO, "/createlocal player default meshid found: " + str$(mymeshid))
         tidPlayer = playerClientCreate(1, mymeshid)
         dataSharedRegisterNoSaveTable("1")
         debugWrite(DEBUGINFO, "/createlocalplayer tidPlayer set to " + str$(tidPlayer))
         mydbpid = intval(dataSharedGetFieldFast(tidPlayer,"dbpid"))
         systemSharedGameModeAdd(SYSTEM_MODE_AV_SELECT)
         cameraClientPaperDoll(mydbpid)
      endcase
      
      case "/deletelocalplayer"
         playerClientDestroy(tidPlayer)
         systemSharedGameModeRemove(SYSTEM_MODE_AV_SELECT)
         debugWrite(DEBUGINFO, "/deletelocalplayer called playerClientDestory for " + str$(tidPlayer))
      endcase
                 
      case "/createavatar"
         netClientSendCreateAvatarRequest()
      endcase
      
      case "/getavatarlist"
            netClientSendAvatarList()
      endcase
      
      case "/switchavatar"
            netClientSendAvatarDetailsSelf(get split word$(2))
      endcase
      
      case "/enterworld"
         REM FOR NOW, JUST LOADING A DEFAULT WORLD
         REM CLIENT-SIDE, NEXT STEP IS TO MAKE A 
         REM ENTER WORLD REQUEST TO SERVER
         terrainClientLoad("Piston Ridge")
         debugWrite(DEBUGINFO,"/enterworld tidPlayer is currently " + str$(tidPlayer))
         playerClientTeleport(tidPlayer)
         systemSharedGameModeRemove(SYSTEM_MODE_AV_SELECT)
         systemSharedGameModeAdd(SYSTEM_MODE_LOGON_COMPLETE)
         
         REM WE USE SwitchToWindow SPECIFICALLY TO REMOVE
         REM ALL EXISTING WINDOWS
         guiClientSwitchToWindow("windowMain")
         guiClientShowWindow("windowChat")
      endcase
            
      case default
            REM NOT A CLIENT COMMAND, SEND TO SERVER
            netClientSendCommand(cmd$)
            debugWrite(DEBUGINFO,"Command not found, sent to server.")
      endcase

   endselect

endfunction
