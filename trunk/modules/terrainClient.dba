REM *** Include File: modules\collisionClient.dba ***
REM Created: 7/4/2010 11:03:07 AM
REM
REM Included in Project: C:\Program Files\The Game Creators\Dark Basic Professional\Projects\worldofomen\worldofomen.dbpro
REM

terrainClientSetup:

   #constant TERRAIN_PATH "media/terrain/"

   global idxTerrain as integer
   global idTerrainTex as integer
   global idTerrainDet as integer


return

function terrainClientLoad(terrainID, detailID)

   if terrainID = 0 or detailID = 0 then exitfunction -1

   if idTerrainTex = 0 then idTerrainTex = systemSharedGetFree(SYSTEM_TYPE_IMAGE, SYSTEM_SEARCH_FAST)
   if idTerrainDet = 0 then idTerrainDet = systemSharedGetFree(SYSTEM_TYPE_IMAGE, SYSTEM_SEARCH_FAST)

   if idxTerrain = 0
      idxTerrain = dataSharedGetFree(DATA_SEARCH_REUSE)
      debugWrite(DEBUGINFO,"terrainLoad: idxTerrain set to " + str$(idxTerrain))
   endif

   dataObject(idxTerrain).class = DATA_CLASS_TERRAIN

   if dataObject(idxTerrain).dbpid = 0
      mydbpid = dataSharedSetKeys(idxTerrain, 0, 0, 0)
      debugWrite(DEBUGINFO,"terrainLoad: dbpid found for terrain " + str$(mydbpid))
   else
      if object exist(dataObject(idxTerrain).dbpid)
         delete object dataObject(idxTerrain).dbpid
      endif
      mydbpid = dataObject(idxTerrain).dbpid
   endif


   texFileName$ = TERRAIN_PATH + str$(terrainID) + "_TX.jpg"
   mapFileName$ = TERRAIN_PATH + str$(terrainID) + "_HF.bmp"
   detFileName$ = TERRAIN_PATH + str$(detailID) + "_DT.tga"

   load image texFileName$, idTerrainTex
   load image detFileName$, idTerrainDet

   make object terrain mydbpid
   set terrain heightmap mydbpid, mapFileName$
   set terrain scale mydbpid, 3, 0.6, 3
   set terrain split mydbpid, 4
   set terrain tiling mydbpid, 4
   set terrain light mydbpid, -0.5, -0.5, 0.5, 1, 1, 1, .25
   set terrain texture mydbpid, idTerrainTex, idTerrainDet
   build terrain mydbpid

   collisionClientSet(mydbpid, COLLISION_TYPE_COMPLEX, COLLISION_GROUP_WORLD)

   systemSharedGameModeAdd(SYSTEM_MODE_TERRAIN)

endfunction idxTerrain

function terrainClientUpdate()

   update terrain

endfunction

function terrainClientUnload()

   if idTerrainTex > 0
      if image exist(idTerrainTex)
         delete image idTerrainTex
      endif
   endif

   if idTerrainDet > 0
      if image exist(idTerrainDet)
         delete image idTerrainDet
      endif
   endif

   if idxTerrain > 0 and idxTerrain < array count(dataObject())
      if object exist(dataObject(idxTerrain).dbpid)
         collisionClientDelete(dataObject(idxTerrain).dbpid)
         destroy terrain dataObject(idxTerrain).dbpid
      endif
   endif

   systemSharedGameModeRemove(SYSTEM_MODE_TERRAIN)

endfunction




