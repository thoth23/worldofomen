REM *** Include File: modules\collisionClient.dba ***
REM Created: 7/4/2010 11:03:07 AM
REM
REM Included in Project: C:\Program Files\The Game Creators\Dark Basic Professional\Projects\worldofomen\worldofomen.dbpro
REM

terrainClientSetup:

   global dbpidTerrain as integer
   global idTerrainTex as integer
   global idTerrainDet as integer
   
   #constant TERRAIN_PATH "media/terrain/"
   mediaClientRegisterPath(TERRAIN_PATH)
   
return

function terrainClientLoad(terrainID, detailID)

   if terrainID = 0 or detailID = 0 then exitfunction -1

   if idTerrainTex = 0 then idTerrainTex = systemSharedGetFree(SYSTEM_TYPE_IMAGE, SYSTEM_SEARCH_FAST)
   if idTerrainDet = 0 then idTerrainDet = systemSharedGetFree(SYSTEM_TYPE_IMAGE, SYSTEM_SEARCH_FAST)

   if dbpidTerrain = 0
      dbpidTerrain = systemSharedGetFree(SYSTEM_TYPE_OBJECT,DATA_SEARCH_REUSE)
      debugWrite(DEBUGINFO,"terrainLoad: dbpidTerrain set to " + str$(dbpidTerrain))
   endif

   if object exist(dbpidTerrain)
      delete object dbpidTerrain
   endif
   
   texFileName$ = TERRAIN_PATH + str$(terrainID) + "_TX.jpg"
   mapFileName$ = TERRAIN_PATH + str$(terrainID) + "_HF.bmp"
   detFileName$ = TERRAIN_PATH + str$(detailID) + "_DT.tga"

   load image texFileName$, idTerrainTex
   load image detFileName$, idTerrainDet

   make object terrain dbpidTerrain
   set terrain heightmap dbpidTerrain, mapFileName$
   set terrain scale dbpidTerrain, 3, 0.6, 3
   set terrain split dbpidTerrain, 4
   set terrain tiling dbpidTerrain, 4
   set terrain light dbpidTerrain, -0.5, -0.5, 0.5, 1, 1, 1, .25
   set terrain texture dbpidTerrain, idTerrainTex, idTerrainDet
   build terrain dbpidTerrain

   collisionClientSet(dbpidTerrain, COLLISION_TYPE_COMPLEX, COLLISION_GROUP_WORLD)

   systemSharedGameModeAdd(SYSTEM_MODE_TERRAIN)

endfunction dbpidTerrain

function terrainClientUpdate()

   update terrain

endfunction

function terrainClientUnload()

   if idTerrainTex > 0
      if image exist(idTerrainTex)
         delete image idTerrainTex
      endif
   endif

   if idTerrainDet > 0
      if image exist(idTerrainDet)
         delete image idTerrainDet
      endif
   endif

   if dbpidTerrain > 0
      if object exist(dbpidTerrain)
         collisionClientDelete(dbpidTerrain)
         destroy terrain dbpidTerrain
      endif
   endif

   systemSharedGameModeRemove(SYSTEM_MODE_TERRAIN)

endfunction




