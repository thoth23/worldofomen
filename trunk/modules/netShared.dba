REM *** Include File: modules\netShared.dba ***
REM Created: 7/4/2010 10:55:08 AM
REM
REM Included in Project: C:\Program Files\The Game Creators\Dark Basic Professional\Projects\worldofomen\worldofomen.dbpro
REM

netSharedSetup:

   #constant NET_SERVER_NAME_DEFAULT "127.0.0.1"

   global NET_SERVER_NAME as string
   NET_SERVER_NAME = dataSharedGetField("netShared", "netServerName", "", DATA_CREATE)

   global NET_SERVER_PORT as integer
   NET_SERVER_PORT = intval(dataSharedGetField("netShared", "netServerPort", "9669", DATA_CREATE))

   global NET_INSTANCES as integer
   NET_INSTANCES = intval(dataSharedGetField("netShared", "netInstances", "1", DATA_CREATE))

   global NET_THREADS as integer
   NET_THREADS = intval(dataSharedGetField("netShared", "netThreads", "0", DATA_CREATE))

   global NET_UDP_MODE as integer
   NET_UDP_MODE = intval(dataSharedGetField("netShared", "netUDPMode", "2", DATA_CREATE))

   global NET_TIMEOUT_SECONDS as integer
   NET_TIMEOUT_SECONDS = intval(dataSharedGetField("netShared", "netTimeOutSeconds", "10", DATA_CREATE))

   global NET_UDP_UPDATE_INTERVAL as integer
   NET_UDP_UPDATE_INTERVAL = intval(dataSharedGetField("netShared", "netUDPUpdateInterval", "250", DATA_CREATE))

   global NET_UDP_HEARTBEAT_INTERVAL as integer
   NET_UDP_HEARTBEAT_INTERVAL = intval(dataSharedGetField("netShared", "netUDPHeartbeatInterval", "5000", DATA_CREATE))

   global NET_TCP_RECONNECT_INTERVAL as integer
   NET_TCP_RECONNECT_INTERVAL = NET_UDP_HEARTBEAT_INTERVAL + rnd(2000)

   #constant NET_HEADER_BYTES 12

   global SendPacket as dword
   global RecvPacket as dword

   SendPacket = mn create packet()
   RecvPacket = mn create packet()
   mn set memory size SendPacket,1024
   rem RecVPacket size is set automatically on receive

   global NET_LISTEN_CHANNELS_DEFAULT as string
   NET_LISTEN_CHANNELS_DEFAULT = dataSharedGetField("netShared", "netListenChannelsDefault", "SYSTEM;LOCAL;BROADCAST", DATA_CREATE)

   REM CLIENT TCP OPERATIONS
   #constant NETC_CHAT              1
   #constant NETC_COMMAND           2
   #constant NETC_LISTEN            3
   #constant NETC_MUTE              4
   #constant NETC_RPGID             5

   REM SERVER TCP OPERATIONS
   #constant NETS_CHAT              1001
   #constant NETS_COMMAND           1002
   #constant NETS_LISTEN            1003
   #constant NETS_MUTE              1004
   #constant NETS_RPGID             1005

   REM CLIENT UDP OPERATIONS
   #constant NETC_MOVE 0
   #constant NETC_JUMP 1

   REM SERVER UDP OPERATIONS
   #constant NETS_MOVE 0
   #constant NETS_JUMP 1

return

function netSharedAddHeader(msgID as integer,size as integer)

    REM UTILITY FUNCTION TO INSURE
    REM HEADER IS ALWAYS CORRECT
    REM LENGTH IS UPDATED ON SEND
    REM MINIMUM SIZE IS 12 BYTES
    mn Clear Packet SendPacket
    mn Add Int SendPacket, size
    mn Add Int SendPacket, timer()
    mn Add Int SendPacket, msgID

endfunction

function netSharedGetLANIP()

   local i as integer
   local retval$ as string
   retval$ = ""

   PERFORM CHECKLIST FOR INTERFACES
   for i = 1 to INTERFACE COUNT()
      if retval$ = ""
         if checklist value a(i) <> 0
            retval$ = IP TO STRING$(checklist value a(i))
         endif
      endif
   next

endfunction retval$

function netSharedGetWANIP()

   local i as integer
   local retval$ as string
   retval$ = ""
   local filename$ as string

   filename$ = "temp" + "_" + systemSharedGetRealDateTime("_","_","_") + ".txt"

   if file exist(filename$) then delete file filename$

   load dll "urlmon.dll", 101
   Status = call dll(101, "URLDownloadToFileA", 0, "http://www.whatismyip.com/automation/n09230945.asp", filename$, 0, 0)
   delete dll 101

   if Status = 0
      open to read SYSTEM_FILE_NET, filename$
      read string SYSTEM_FILE_NET, retval$
      close file SYSTEM_FILE_NET
   endif

   if file exist(filename$) then delete file filename$

endfunction retval$
