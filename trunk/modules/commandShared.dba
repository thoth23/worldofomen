REM *** Include File: modules\commandShared.dba ***
REM Created: 7/4/2010 10:59:52 AM
REM
REM Included in Project: C:\Program Files\The Game Creators\Dark Basic Professional\Projects\worldofomen\worldofomen.dbpro
REM

commandSharedSetup:

   #constant COMMAND_CHAR_START   "/"
   #constant COMMAND_CHAR_CMDSEP  ";"
   #constant COMMAND_CHAR_ARGSEP  " "
   #constant COMMAND_CHAR_ARGEMB  "_"

   type commandTokenType
      name$ as string
      value$ as string
   endtype

   global dim commandList() as string
   add to queue commandList()

   global COMMAND_MASKED_FIELDS as string
   COMMAND_MASKED_FIELDS = systemSharedFieldAdd(COMMAND_MASKED_FIELDS,"inputLogonPassword",COMMAND_CHAR_CMDSEP)
   COMMAND_MASKED_FIELDS = systemSharedFieldAdd(COMMAND_MASKED_FIELDS,"inputCreateAccountPassword",COMMAND_CHAR_CMDSEP)

return

function commandSharedParse(dat$ as string)

   if dat$ = ""
      exitfunction
   endif

   if instr(dat$, COMMAND_CHAR_CMDSEP) = 0
      commandSharedTranspose(dat$)
      exitfunction
   endif

   split string dat$,COMMAND_CHAR_CMDSEP

   for i = 1 to split count()
      add to queue commandList()
      commandList() = get split word$(i)
   next

   REM QUEUES ARE FIFO, SO ALWAYS RUN COMMAND 1
   REM AND THEN REMOVE FROM QUEUE
   while array count(commandList()) > 0
      commandSharedTranspose(commandList(1))
      remove from queue commandList()
   endwhile

endfunction

function commandSharedParseStartup()

   local myCL$ as string

   myCL$ = CL$()

   if myCL$ <> ""
      commandSharedParse(myCL$)
   endif

endfunction

function commandSharedTranspose(cmd$ as string)

   REM WE NEED TO SEE IF THE COMMAND CONTAINS ANY FIELD NAMES
   REM AND IF SO, REPLACE THE FIELD NAMES WITH THE VALUES OF THE
   REM FIELDS.  FIELD VALUES WILL BE IN THE INPUT TABLE IN THE DATA
   REM SYSTEM.  ALSO CHECK TO SEE IF THE FIELD IS A MEMBER OF THE
   REM COMMAND_MASKED_FIELDS, AND IF SO, IT SHOULD BE MASKED IN THE LOG
   
   if cmd$ = ""
      exitfunction
   endif

   log$ = cmd$

   split string cmd$,COMMAND_CHAR_ARGSEP

   for i = 1 to split count()
      myKey$ = get split word$(i)
      myVal$ = dataSharedGetField(DATA_TABLE_INPUT, myKey$, myKey$,0)
      if myVal$ <> DATA_KEY_UNDEFINED and myVal$ <> myKey$
         if instr(COMMAND_MASKED_FIELDS,myKey$) > 0
            log$ = replace all$(log$,myKey$,DATA_KEY_HIDDEN)
         else
            log$ = replace all$(log$,myKey$,myVal$)
         endif
         cmd$ = replace all$(cmd$,myKey$,myVal$)
      endif
   next
   
   debugWrite(DEBUGINFO,"commandRun: " + log$)
   commandRun(cmd$)


endfunction

function commandSharedUnMask(cmd$)
   cmd$ = replace all$(cmd$," ","_")
   cmd$ = replace all$(cmd$,"\"," ")
endfunction cmd$

function commandSharedMask(cmd$)
   cmd$ = replace all$(cmd$," ","\")
   cmd$ = replace all$(cmd$,"_"," ")
endfunction cmd$


